plugins {
    id 'net.ltgt.apt' version '0.19'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '0.4.2'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'net.ltgt.apt'
    apply plugin: 'net.ltgt.apt-idea'
    apply plugin: 'org.jetbrains.gradle.plugin.idea-ext'

    dependencies {
        compile 'com.google.dagger:dagger:2.19'
        annotationProcessor 'com.google.dagger:dagger-compiler:2.19'
    }

    project.plugins.withType(JavaPlugin) {
        project.plugins.withId("net.ltgt.apt-idea") {
            project.afterEvaluate {
                project.idea.module.apt.addAptDependencies = false
            }
        }
    }

    group = 'io.leoplatform'
    compileJava {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
        options.encoding = 'UTF-8'
        options.debugOptions.debugLevel = 'source,lines,vars'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url internal_url
        }
    }
}

subprojects {
    dependencies {
        compile(
                'javax.inject:javax.inject:1',
                'ch.qos.logback:logback-classic:1.2.3',
                'org.slf4j:slf4j-api:1.7.25'
        )
        testCompile(
                'org.testng:testng:6.14.3'
        )
    }

    tasks.withType(Test) {
        test {
            useTestNG {
                useDefaultListeners = true
                options.suites("src/test/resources/testng.xml")
            }
            testLogging {
                events "passed", "skipped", "failed"
            }
        }
    }
}
